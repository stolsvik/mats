// Customize the Gradle Wrapper generation task.
wrapper {
    gradleVersion = "6.2"
    // Use "-all"-version of distribution, since IntelliJ works better then..
    setDistributionType(Wrapper.DistributionType.ALL)
}

// NOTE: MATS VERSION is set in gradle.properties

// Spring, ActiveMQ and Jackson JSON versions: Used in several sub projects
ext {
    activeMqVersion = '5.15.+'
    springVersion = '4.+' // Still using Spring 4 as "baseline", but also fully compatible with Spring 5. 2020-11-14.
    // NOTE: Spring started supporting JUnit Jupiter with the release of Spring 5.0.0.
    springVersionForJupiter = '5.+'
    jacksonVersion = '2.11.+'
    slf4jVersion = '1.7.+'

    // For the JUnit system tests, and the JUnit testing tools
    junitVersion = '4.+'
    // For the Jupiter testing tools
    jupiterVersion = '5.+'
    jupiterPlatformVersion = '1.7.+'

    // For testing
    logbackVersion = '1.2.+'
    h2Version = '1.4.+'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'eclipse'

    repositories { mavenCentral() }

    project.buildDir = 'build-gradle'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        // without the -quiet option, the build fails
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.deprecation = true
        options.incremental = true
        //options.compilerArgs += ['-Xlint:all', '-Xlint:-serial', '-Werror']

        // Removing -Werror while making MatsSockets. TODO: Get it back! 2019-12-01
        options.compilerArgs += ['-Xlint:all', '-Xlint:-serial']
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'MATS',
                    'Implementation-Version': archiveVersion
        }
    }

    // Define a task that all subprojects gets. Invoke with "./gradle allDeps | less"
    task allDeps(type: DependencyReportTask) {}

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = 'MATS^3'
                    description = 'Message-based Asynchronous Transactional Staged Stateful Services'
                    url = 'https://github.com/stolsvik/mats'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'stolsvik'
                            name = 'Endre StÃ¸lsvik'
                            email = 'endre@stolsvik.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/stolsvik/mats.git'
                        developerConnection = 'scm:git:ssh:git@github.com:stolsvik/mats.git'
                        url = 'https://github.com/stolsvik/mats'
                    }
                }
                from components.java
            }
        }
    }

    // ------------------------------------------------
    // -- Release configuration. The release file is not checked in to version control, and should
    //    instead be kept locally to configure how to deploy this.
    if (file("${ rootProject.projectDir }/repositories.gradle").exists()) {
        apply from: "${ rootProject.projectDir }/repositories.gradle"
    }

    // ------------------------------------------------
    // -- Boring stuff, should be a plugin.

    task systemInformation {
        doLast {
            println "\n** Java properties [System.getProperties()], sorted:\n"
            System.properties.sort().each { k, v ->
                println("$k = $v")
            }
            println "\n** Environment [System.getenv()], sorted:\n"
            System.env.sort().each { k, v ->
                println("$k = $v")
            }
            println ''
            println "** Java Version:   " + System.getProperty("java.version")
            println "** Groovy Version: " + GroovySystem.getVersion()
            println "** Gradle Version: " + gradle.gradleVersion
        }
    }

    // Eclipse-plugin configuration
    tasks.eclipse.dependsOn(cleanEclipse)
    eclipse {
        // project { natures 'org.springsource.ide.eclipse.gradle.core.nature'; }
        classpath {
            defaultOutputDir = file('build-eclipse')
            file {
                whenMerged { classpath ->
                    // Dump the classpath entries so that we can see them
                    classpath.entries.findAll { entry ->
                        println("Entry: $entry")
                    }
                }
            }
        }
    }
}
