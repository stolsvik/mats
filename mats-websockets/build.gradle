import java.nio.charset.StandardCharsets
import java.time.LocalDateTime

// mats-websockets
plugins {
    id "com.github.node-gradle.node" version "2.1.1"
}

dependencies {
    // Uses the mats-lib project
    api project(":mats-api")

    // Jackson JSON library, which is the serialization mechanism in use for MatsSockets comms
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"

    // Relies on the WebSocket API
    compileOnly 'javax.websocket:javax.websocket-api:1.0'

    // Flyway, for DB preparation and migration for 'ClusterStoreAndForward_SQL'. This is decidedly optional, and
    // users of the API should add this themselves if they want it.
    compileOnly 'org.flywaydb:flyway-core:6.1.+'

    // The SFL4J API. Since you should provide the SLF4J implementation yourself, we use compileOnly.
    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"

    // :: TEST

    // We need an implementation of the MatsSerializer, the MatsSerializerJson
    testImplementation project(':mats-serial-json')

    // Testing tools for Mats
    testImplementation project(":mats-test-junit")

    // Base Jetty Server
    testImplementation 'org.eclipse.jetty:jetty-server:9.+'
    // .. Jetty Servlets
    testImplementation 'org.eclipse.jetty:jetty-webapp:9.+'
    // .. Jetty: To get the @WebListener etc. annotation scanning.
    testImplementation 'org.eclipse.jetty:jetty-annotations:9.+'
    // .. Jetty: JSR-356 javax.websocket implementation for Jetty
    testImplementation 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.+'

    // H2 Database
    testImplementation "com.h2database:h2:$h2Version"

    // Add flyway, since it is not transitive from the lib.
    testImplementation 'org.flywaydb:flyway-core:6.1.+'

    // Logging
    testImplementation "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
}

// Task to start a MatsTestWebsocketServer for integration tests. We will monitor the log until we get the expected number
// of ws urls, that other tasks can then depend on
task startMatsTestWebsocketServer(dependsOn: [configurations.testRuntimeClasspath, compileTestJava, processTestResources]) {
    ext {
        wsUrls = []
    }

    doLast {
        int numberOfServers = 3
        String serverClassname = "com.stolsvik.mats.websocket.MatsTestWebsocketServer"
        logger.info("Starting MatsTestWebsocketServer");
        List<String> cmd = [
                "${System.getenv("JAVA_HOME")}/bin/java",
                "-classpath", sourceSets.test.runtimeClasspath.asPath,
                serverClassname,
                numberOfServers.toString()
        ]
        Process server = cmd.execute()

        // Keep a log file of the server output
        File log = new File("$buildDir/logs/matsSocketServer-${LocalDateTime.now().withNano(0)}.log")
        log.parentFile.mkdirs();

        Reader reader = new InputStreamReader(server.inputStream, StandardCharsets.UTF_8)
        log.withWriterAppend { out ->
            String line;
            while (wsUrls.size < numberOfServers && (line = reader.readLine()) != null) {
                out.writeLine(line)
                logger.info("TestServer: $line")
                if (line.contains('HOOK_FOR_GRADLE_WEBSOCKET_URL')) {
                    int urlStart = line.indexOf("#[")
                    int urlEnd = line.indexOf("]#")
                    if (urlStart > -1 && urlEnd > urlStart) {
                        String url = line.substring(urlStart + 2, urlEnd)
                        wsUrls.add(url)
                        logger.info("!! Registering WS URL: $url");
                    }
                }
            }
        }
        if (wsUrls.isEmpty()) {
            server.errorStream.eachLine { logger.error(it) }
            logger.error("Failed to execute: [${cmd.join(" ")}]")
            throw new GradleException("Failed to start $serverClassname, check log above for command.")
        }
        logger.info("$serverClassname started");

        // Fork a new thread to just keep reading and logging the MatsTestWebsocketServer
        new Thread({
            log.withWriterAppend { out ->
                reader.eachLine { line ->
                    out.writeLine(line)
                }
            }
        }, "MatsTestWebsocketServer-logprinter").start()
    }
}

// Stop the MatsTestWebsocketServer, this is done by inspecting the wsUrls field on the start task,
// and creating a url to the shutdown page based on the first websocket url. The shutdown page is
// a servlet that will do System.exit(0) to shutdown the server.
task stopMatsTestWebsocketServer(dependsOn: startMatsTestWebsocketServer) {
    doLast {
        String shutdownUrl = startMatsTestWebsocketServer.wsUrls[0]
                .replace("ws://", "http://")
                .replace("/matssocket", "/shutdown");
        logger.info("Shutting down MatsTestWebsocketServer by invoking '$shutdownUrl")
        String response = new URL(shutdownUrl).text
        logger.info("Response: [${response.trim()}]")
    }
}

// Make sure that the startMatsTestWebsocketServer is finalized and shut down.
startMatsTestWebsocketServer.finalizedBy(stopMatsTestWebsocketServer)

apply from: 'javascript.gradle'
apply from: 'dart.gradle'